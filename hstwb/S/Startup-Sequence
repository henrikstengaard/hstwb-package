; Startup-Sequence
; ----------------
; 
; Author: Henrik Noerfjand Stengaard
; Date: 2017-12-14


; Add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" "  


; Get mouse button pressed
C:GetMouseInput LOCAL


; Run configure setup, if right mouse button is hold down during startup (for recovery)
IF $MouseInput EQ 2
  SKIP configuresetup
ENDIF

SKIP startup

; Configure Setup
LAB configuresetup

cls

C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET
Assign >NIL: T: RAM:T

cd SYS:Programs/Configuration
execute Configure-Setup

echo ""
echo "Rebooting in 10 seconds..."
wait 10
totalreset


; Startup
; -------
LAB startup


; Execute remove AGS2 favourites
IF EXISTS SYS:Prefs/HstWB/RemoveAgs2Favourites
  execute S:RemoveAgs2Favourites
  delete >NIL: S:RemoveAgs2Favourites
ENDIF


; Execute remove AMS favourites
IF EXISTS SYS:Prefs/HstWB/RemoveAmsFavourites
  execute S:RemoveAmsFavourites
  delete >NIL: S:RemoveAmsFavourites
ENDIF


; Execute Blizzard script, if "Use-Blizzard" file exists
IF EXISTS SYS:Prefs/HstWB/Use-Blizzard
  execute S:Blizzard
ENDIF


; Execute ACA script, if "Use-ACA" file exists
IF EXISTS SYS:Prefs/HstWB/Use-ACA
  execute S:ACA
ENDIF


; Execute Furia script, if "Use-Furia" file exists
IF EXISTS SYS:Prefs/HstWB/Use-Furia
  execute S:Furia
ENDIF


; Load scsi.device and icon.library using loadmodule, if not "Use-Blizzard" file exists
IF NOT EXISTS SYS:Prefs/HstWB/Use-Blizzard
  ; Add loadmodule scsi.device, if "Load-ScsiDevice" and "DEVS:scsi.device" files exists
  IF EXISTS SYS:Prefs/HstWB/Load-ScsiDevice
    IF EXISTS DEVS:scsi.device
	  ; Load scsi.device, if current loaded scsi.device is less than v43.45
      Version >NIL: "scsi.device" 43 45
      IF WARN
        echo "C:LoadModule DEVS:scsi.device" NOLINE >RAM:Load-Modules
      EndIF
    EndIF
  EndIF

  ; Add loadmodule icon.library, if "Load-IconLibrary" file exists
  IF EXISTS SYS:Prefs/HstWB/Load-IconLibrary
    IF EXISTS LIBS:icon.library
	  ; Load icon.library, if current loaded icon.library is less than v46.x
      Version >NIL: "icon.library" 46
      IF WARN
        IF EXISTS RAM:Load-Modules
          echo " LIBS:icon.library" NOLINE >>RAM:Load-Modules
        Else
          echo "C:LoadModule LIBS:icon.library" NOLINE >RAM:Load-Modules
        EndIF
      EndIF
    EndIF
  EndIF
	
  IF EXISTS RAM:Load-Modules
    execute RAM:Load-Modules
  EndIF
ENDIF


; Run setpatch quiet, if setpatch is less than v44.13. Otherwise run setpatch noromupdate quiet
Version >NIL: C:SetPatch 44 13 FILE
IF WARN
  C:SetPatch QUIET
ELSE
  C:SetPatch NOROMUPDATE QUIET
ENDIF


C:Version >NIL:
C:AddBuffers >NIL: DF0: 15
FailAt 21


C:anticlick
C:PatchRam
C:MinStack 8192
C:TagLiFE
C:PatchWB


C:MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
C:Copy ENVARC:~(#?.info) ENV: ALL QUIET


Resident >NIL: C:Assign PURE
Resident >NIL: C:Execute PURE

Assign >NIL: T: RAM:T
Assign >NIL: CLIPS: RAM:Clipboards
Assign >NIL: REXX: S:
Assign >NIL: PRINTERS: DEVS:Printers
Assign >NIL: KEYMAPS: DEVS:Keymaps
Assign >NIL: LOCALE: SYS:Locale
Assign >NIL: LIBS: SYS:Classes ADD
Assign >NIL: HELP: LOCALE:Help DEFER
Assign >NIL: DOPUS: SYS:Programs/DirOpus4

; Assign Startup
execute S:Assign-Startup

; Menu Startup
IF EXISTS S:Menu-Startup
  execute S:Menu-Startup
ENDIF

; Start Amiga Boot Selector, if "Start-ABS" file exists
IF EXISTS SYS:Prefs/HstWB/Start-ABS
	C:abs
	execute S:Boot.abs
	SKIP end
ENDIF

; Start Multiboot, if "Start-Multiboot" file exists
IF EXISTS SYS:Prefs/HstWB/Start-Multiboot
	execute S:Multiboot
	SKIP end
ENDIF

; Start Arcade Game Selector 2 Games script, if "Start-AGS2Games" file exists
IF EXISTS SYS:Prefs/HstWB/Start-AGS2Games
  IF EXISTS S:AGS2Menu
    execute S:AGS2Menu "AGS2Games"
  ELSE
    REQUESTCHOICE "Error" "AGS2 Menu is not installed!" "OK" >NIL:
  ENDIF
ENDIF

; Start Arcade Game Selector 2 Demos script, if "Start-AGS2Demos" file exists
IF EXISTS SYS:Prefs/HstWB/Start-AGS2Demos
  IF EXISTS S:AGS2Menu
    execute S:AGS2Menu "AGS2Demos"
  ELSE
    REQUESTCHOICE "Error" "AGS2 Menu is not installed!" "OK" >NIL:
  ENDIF
ENDIF

; Start Workbench
execute S:WB


; End
LAB end
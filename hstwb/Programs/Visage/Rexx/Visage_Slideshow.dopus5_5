/* Visage_Slideshow for Directory Opus 5.5 and Visage.
   by Leo 'Nudel' Davidson for Gods'Gift Utilities
   email: leo.davidson@keble.oxford.ac.uk  www: http://users.ox.ac.uk/~kebl0364

 Visage is (C)1996 by Magnus Holmgren (Internet: cmh@lls.se or cmh@augs.se,
 Fidonet: 2:204/204.6) and available on Aminet or from Magnus' WWW Page
 (http://www.lls.se/~cmh). Many thanks to Magnus for a great picture viewer,
 especially the rare random-slideshow and xpk-decrypt options.

$VER: Visage_Slideshow.dopus5 1.10 (13.6.96)

   This simple script will call "Visage" to display a slideshow of all
   pictures in the source-lister's path, in random order. Optionally, a
   requester will appear so that you can give it a password to xpk-decrypt the
   pictures with, if required.

   To change the default amount of time pictures are displayed for, alter the
   "Visage_Delay" variable below (time in seconds).

   This script has the ability to disable SwazBlanker for the duration of
   the slideshow. For this to work you must have a program called "handlecx"
   in your path and you must enable it by having the "DisSwaz" switch below
   as "YES". If you don't run SwazBlanker or don't have handlecx, make sure
   you set it to "NO" or things might not work. Users of MCP's blanker system
   should replace the two occurances of 'handlecx enable "SwazBlanker"'
   with 'handlecx enable "MCP"'

Call as:
------------------------------------------------------------------------------
ARexx   DOpus5:ARexx/Visage_Slideshow.dopus5 {Qp} {Ql} {s} [NoPass] [<Time>]
------------------------------------------------------------------------------
Turn off all switches.
"[]" means this part is optional.
Arguments must be given in the order shown.

NOTE: Command-line changed in v1.10

NoPass: Tells the script not to prompt for a password (no decryption).
<Time>: Override the default delay-time (seconds) between pictures.

//- Path to Visage command -------------------------------------------------*/
Visage_Path  = "DH0:Tools/Art/Visage"
/*- Default Delay variable -------------------------------------------------*/
Visage_Delay = 10
/*- If you run SwazBlanker and have "handlecx" in your path, put "YES" -----*/
DisSwaz = "NO"
/*--------------------------------------------------------------------------*/
options results
options failat 99
signal on syntax;signal on ioerr                /* Error trapping */
parse arg DOpusPort source_handle.0 '"' source_path.0 '"' opt1 opt2 .

If DOpusPort="" THEN Do
	Say "Not correctly called from Directory Opus 5!"
	Say "Load this ARexx script into an editor for more info."
	EXIT
	END
If ~Show("P",DOpusPort) Then Do
	Say DOpusPort "is not a valid port."
	EXIT
	End
Address value DOpusPort

dopus version
If ( result='RESULT' | translate(result,'.',' ') < 5.1218 ) then do
	lister request source_handle.0 '"This script requires DOpus v5.5 or greater." OK'
	EXIT
	end

Delay = Visage_Delay
AskPass = "Y"
If opt1~="" then do
	If upper(opt1)="NOPASS" then do
		AskPass = "N"
		opt1 = opt2
		End
	If Datatype(opt1,"N") then
		Delay = opt1
	End

Visage_Password = ""
If AskPass = "Y" then do
	lister getstring source_handle.0 '"Enter password, if required" Secure 256 "" Okay|Cancel'
	If DOPUSRC = 0 Then Exit
	If Result ~= "" Then Visage_Password = ' PASSWORD "' || RESULT || '"'
	/* Note: Result not cleared in DOpus versions < 5.5 */
	End

If DisSwaz = "YES" Then Address command 'handlecx disable "SwazBlanker"'

address command Visage_Path '"'source_path.0'#?" DELAY' Delay Visage_Password "NOBUSY FOREVER QUIET RANDOM WAITFORPIC CENTRE"

If DisSwaz = "YES" Then Address command 'handlecx enable "SwazBlanker"'

syntax:;ioerr:                          /* In case of error, jump here */
EXIT

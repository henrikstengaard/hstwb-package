; Configure Menus Script
; ----------------------
; Author: Henrik Noerfjand Stengaard
; Date: 2017-01-04
;
; This script configures menus.


; Main Menu
; ---------
LAB mainmenu

echo "AGS2 Games" >T:mainmenu
echo "AGS2 Demos" >>T:mainmenu
echo "Exit" >>T:mainmenu

; Show main menu
C:ReqList CLONERT BUTTONS I T:mainmenu O ENV:mainmenu H "Configure Menus"
delete >NIL: T:mainmenu

; AGS2 Games
IF $mainmenu eq "1" 
	SKIP ags2games
ENDIF 

; AGS2 Demos
IF $mainmenu eq "2" 
	SKIP ags2demos
ENDIF 

SKIP end


; AGS2 Games
; ----------
LAB ags2games

; Show error message and go back to main menu, if S:AGSMenu.data file doesn't exist
IF NOT EXISTS "S:AGS2Menu.data"
  REQUESTCHOICE "Error" "AGS2 menu data file 'S:AGS2Menu.data'*Ndoesn't exist!" "OK" >NIL:
  SKIP BACK mainmenu
ENDIF

; Show error message and go back to main menu, if S:AGSMenu.data file doesn't contain 'AGS2GamesMenuDir'
search "S:AGS2Menu.data" "AGS2GamesMenuDir=" NONUM >T:_ags2gamesmenudir
IF WARN
  REQUESTCHOICE "Error" "AGS2 menu data file 'S:AGS2Menu.data'*Ndoesn't contain 'AGS2GamesMenuDir'!" "OK" >NIL:
  SKIP BACK mainmenu
ENDIF

set ags2menuname "AGS2 Games"
set ags2menupath `sed "s/^AGS2GamesMenuDir=//" T:_ags2gamesmenudir`
delete "T:_ags2gamesmenudir" >NIL:

SKIP ags2menu


; AGS2 Demos
; ----------
LAB ags2demos

; Show error message and go back to main menu, if S:AGSMenu.data file doesn't exist
IF NOT EXISTS "S:AGS2Menu.data"
  REQUESTCHOICE "Error" "AGS2 menu data file 'S:AGS2Menu.data'*Ndoesn't exist!" "OK" >NIL:
  SKIP BACK mainmenu
ENDIF

; Show error message and go back to main menu, if S:AGSMenu.data file doesn't contain 'AGS2DemosMenuDir'
search "S:AGS2Menu.data" "AGS2DemosMenuDir=" NONUM >T:_ags2demosmenudir
IF WARN
  REQUESTCHOICE "Error" "AGS2 menu data file 'S:AGS2Menu.data'*Ndoesn't contain 'AGS2DemosMenuDir'!" "OK" >NIL:
  SKIP BACK mainmenu
ENDIF

set ags2menuname "AGS2 Demos"
set ags2menupath `sed "s/^AGS2DemosMenuDir=//" T:_ags2demosmenudir`
delete "T:_ags2demosmenudir" >NIL:


; AGS2 Menu
; ---------
LAB ags2menu

; Build ags2 menu
echo "Add" >T:ags2menu
echo "Rename" >>T:ags2menu
echo "Delete" >>T:ags2menu
echo "Show Existing" >>T:ags2menu
echo "Show All" >>T:ags2menu
echo "Hide All" >>T:ags2menu
echo "Back" >>T:ags2menu

; Show ags2 menu
C:ReqList CLONERT BUTTONS I T:ags2menu O ENV:ags2menu H "$ags2menuname"
delete >NIL: T:ags2menu

; Add
IF $ags2menu eq "1" 
	SKIP ags2add
ENDIF 

; Rename
IF $ags2menu eq "2" 
	SKIP ags2rename
ENDIF 

; Delete
IF $ags2menu eq "2" 
	SKIP ags2delete
ENDIF 

; Show existing
IF $ags2menu eq "4" 
  SKIP ags2showexisting
ENDIF 

; Show all
IF $ags2menu eq "5" 
  SKIP ags2showall
ENDIF 

; Hide all
IF $ags2menu eq "6" 
  SKIP ags2hideall
ENDIF 

SKIP BACK mainmenu


; AGS2 Add
; --------
LAB ags2add

; Select AGS2 run file
set runfile ""
set runfile `REQUESTFILE TITLE "Select AGS2 run file" NOICONS`


; End, if run file doesn't exist 
IF NOT EXISTS "$runfile"
  echo "Error: Run file '$runfile' doesn't exist"
  SKIP end
ENDIF


; Get run dir
set rundir `dirname "$runfile"`
echo "$rundir" >T:_rundir
search T:_rundir ":" NONUM >NIL:
IF WARN
  set rundir "$rundir:"
ENDIF

; Get run filename
echo "$runfile" >T:_runfile
rep T:_runfile ":" ":/"
set runfilename `type T:_runfile`
set runfilename `basename "$runfilename"`
echo "$runfile" >T:_runfile
echo "$runfilename" >T:_runfilename


; Check, if run file is HstWB Menu Item
; -------------------------------------

; Skip, if run filename doesn't contain "_runfilename"
search T:_runfilename "hstwbmenuitem" NONUM >NIL:
IF WARN
  SKIP whdloadslave
ENDIF

; Skip, if run file doesn't contain ".data"
search T:_runfilename ".data" NONUM >NIL:
IF WARN
  SKIP whdloadslave
ENDIF

; Skip, if HstWB Menu Item run file doesn't exist
set hstwbmenuitem ""
set hstwbmenuitem `sed "s/\.data$//" T:_runfilename`
IF NOT EXISTS "$rundir/$hstwbmenuitem.run"
  SKIP whdloadslave
ENDIF

; Set AGS2 menu item variables
search "$runfile" "RunFile=" NONUM >T:_hstwbmenuitemdata
IF WARN
  SKIP whdloadslave
ENDIF
set hstwbrunfile `sed "s/^RunFile=//" T:_hstwbmenuitemdata`

search "$runfile" "AGS2Name=" NONUM >T:_hstwbmenuitemdata
IF WARN
  SKIP whdloadslave
ENDIF

set ags2name `sed "s/^AGS2Name=//" T:_hstwbmenuitemdata`
set ags2runfile "$rundir/$hstwbrunfile"
set ags2runscriptfile "$rundir/$hstwbmenuitem.run"

SKIP addags2menuitem


; Check, if run file is whdload slave
; -----------------------------------
LAB whdloadslave

; Skip, if run filename doesn't end with ".slave"
search T:_runfilename ".slave" NONUM >NIL:
IF WARN
  SKIP startrunfile
ENDIF


; Build ags2 run script using AGS2 whdload run template
copy >NIL: S:AGS2WhdloadRunTemplate T:_ags2runscript
rep T:_ags2runscript "[*$RunDir]" "$rundir"
rep T:_ags2runscript "[*$RunFileName]" "$runfilename"

; Set HstWB menu item variables
echo "$runfilename" >T:_ags2name
set ags2name `sed "s/\.slave$//" T:_ags2name`
set ags2runscriptfile "T:_ags2runscript"
set ags2runfile "$runfile"

SKIP addags2menuitem


; Start run file
; --------------
LAB startrunfile

echo "cd *"$rundir*"" >T:ags2runscript
echo "$runfilename" >>T:ags2runscript
set ags2name "$runfilename"
set ags2runscriptfile "T:ags2runscript"
set ags2runfile "$runfile"


; Add AGS2 Menu Item
; -------------------
LAB addags2menuitem
set ags2nameerror ""

LAB enterags2name
set ags2name `RequestString "AGS2 Name" "Enter name for AGS2 (max 26 chars)*N$ags2nameerror" default="$ags2name"`

; Get ags2 index name
echo "$ags2name" >T:_ags2name
sed "s/^\(.\)/echo *"\1*";/" T:_ags2name >T:_ags2indexname
set ags2indexname `execute T:_ags2indexname`
echo "$ags2indexname" >T:_ags2indexname
set ags2indexname `sed "s/^[0-9]/0/" T:_ags2indexname`
IF "$ags2indexname" EQ ""
  set ags2indexname "0"
ENDIF

; Trim ags2 filename to length of 26 chars
cut -c 1-26 T:_ags2name >T:_ags2name2
set ags2name `tail -n1 T:_ags2name2`

; Make AGS2 index directory, if it doesn't exist
IF NOT EXISTS "$ags2menupath/$ags2indexname.ags"
  makedir "$ags2menupath/$ags2indexname.ags"
ENDIF

; Skip to enter ags2 name, if ags2 file name already exists
IF EXISTS "$ags2menupath/$ags2indexname.ags/$ags2name.run"
  set ags2nameerror "Error: AGS2 name '$ags2name' already exists!"
  SKIP enterags2name back
ENDIF
IF EXISTS "$ags2menupath/$ags2indexname.ags/$ags2name.ru_"
  set ags2nameerror "Error: AGS2 name '$ags2name' already exists!"
  SKIP enterags2name back
ENDIF

; Build ags2 run pre template
copy >NIL: S:AGS2RunPreTemplate T:_ags2runpretemplate
rep T:_ags2runpretemplate "[*$RunFile]" "$ags2runfile"
rep T:_ags2runpretemplate "[*$AGS2IndexName]" "$ags2indexname"
rep T:_ags2runpretemplate "[*$AGS2Name]" "$ags2name"

; Build ags2 menu item
echo "" NOLINE >"$ags2menupath/$ags2indexname.ags/$ags2name.run"
type "T:_ags2runpretemplate" >>"$ags2menupath/$ags2indexname.ags/$ags2name.run"
echo "" >>"$ags2menupath/$ags2indexname.ags/$ags2name.run"
type "$ags2runscriptfile" >>"$ags2menupath/$ags2indexname.ags/$ags2name.run"
echo "" >>"$ags2menupath/$ags2indexname.ags/$ags2name.run"
type "S:AGS2RunPostTemplate" >>"$ags2menupath/$ags2indexname.ags/$ags2name.run"

; Clean temp files
delete T:_runfile >NIL:
delete T:_rundir >NIL:
delete T:_runfilename >NIL:
delete T:_ags2name >NIL:
delete T:_ags2name2 >NIL:
delete T:_ags2indexname >NIL:
delete T:_ags2runpretemplate >NIL:
IF EXISTS T:_hstwbmenuitemdata
  delete T:_hstwbmenuitemdata >NIL:
ENDIF
IF EXISTS T:_ags2runscript
  delete T:_ags2runscript >NIL:
ENDIF

SKIP BACK ags2menu


; AGS2 Rename
; -----------
LAB ags2rename

fsearch "$ags2menupath" PAT #?.ags >T:_ags2indexnames1
sed "s/.ags*$//" "T:_ags2indexnames1" >"T:_ags2indexnames2"

C:ReqList CLONERT I "T:_ags2indexnames2" O ENV:ags2indexname H "$ags2menuname"

SKIP BACK ags2menu


; AGS2 Delete
; -----------
LAB ags2delete

SKIP BACK ags2menu


; AGS2 Show Existing
; ------------------
LAB ags2showexisting

; Clear screen
echo "*ec"

fsearch "$ags2menupath" PAT #?.ru#? ALL >T:_ags2menuitemrunfiles1
sed "s/^\(.\)/execute S:AGS2RunUpdate *"\1/" "T:_ags2menuitemrunfiles1" >"T:_ags2menuitemrunfiles2"
sed "s/\(.\)$/\1*" *"EXIST*"/" "T:_ags2menuitemrunfiles2" >"T:_ags2menuitemrunfiles3"
rep T:_ags2menuitemrunfiles3 "execute S:AGS2RunUpdate *"$ags2menupath/.Settings.ags" ";"
echo "Updating $ags2menuname to only show existing..."
echo "(this can take several minutes)"
execute "T:_ags2menuitemrunfiles3"
echo "Done"
delete T:_ags2menuitemrunfiles#? >NIL:

SKIP ags2indexupdate


; AGS2 Show All
; -------------
LAB ags2showall

; Clear screen
echo "*ec"

fsearch "$ags2menupath" PAT #?.ru_ ALL >T:_ags2menuitemrunfiles1
sed "s/^\(.\)/execute S:AGS2RunUpdate *"\1/" "T:_ags2menuitemrunfiles1" >"T:_ags2menuitemrunfiles2"
sed "s/\(.\)$/\1*" *"SHOW*"/" "T:_ags2menuitemrunfiles2" >"T:_ags2menuitemrunfiles3"
rep T:_ags2menuitemrunfiles3 "execute S:AGS2RunUpdate *"$ags2menupath/.Settings.ags" ";"
echo "Updating $ags2menuname to show all..."
echo "(this can take several minutes)"
execute "T:_ags2menuitemrunfiles3"
echo "Done"
delete T:_ags2menuitemrunfiles#? >NIL:

SKIP ags2indexupdate


; AGS2 Hide All
; -------------
LAB ags2hideall

; Clear screen
echo "*ec"

fsearch "$ags2menupath" PAT #?.run ALL >T:_ags2menuitemrunfiles1
sed "s/^\(.\)/execute S:AGS2RunUpdate *"\1/" "T:_ags2menuitemrunfiles1" >"T:_ags2menuitemrunfiles2"
sed "s/\(.\)$/\1*" *"HIDE*"/" "T:_ags2menuitemrunfiles2" >"T:_ags2menuitemrunfiles3"
rep T:_ags2menuitemrunfiles3 "execute S:AGS2RunUpdate *"$ags2menupath/.Settings.ags" ";"
echo "Updating $ags2menuname to hide all..."
echo "(this can take several minutes)"
execute "T:_ags2menuitemrunfiles3"
echo "Done"
delete T:_ags2menuitemrunfiles#? >NIL:


; AGS2 Index Update
; -----------------
LAB ags2indexupdate

echo "Updating $ags2menuname indexes..."
execute S:AGS2IndexUpdate "$ags2menupath" "0"
execute S:AGS2IndexUpdate "$ags2menupath" "A"
execute S:AGS2IndexUpdate "$ags2menupath" "B"
execute S:AGS2IndexUpdate "$ags2menupath" "C"
execute S:AGS2IndexUpdate "$ags2menupath" "D"
execute S:AGS2IndexUpdate "$ags2menupath" "E"
execute S:AGS2IndexUpdate "$ags2menupath" "F"
execute S:AGS2IndexUpdate "$ags2menupath" "G"
execute S:AGS2IndexUpdate "$ags2menupath" "H"
execute S:AGS2IndexUpdate "$ags2menupath" "I"
execute S:AGS2IndexUpdate "$ags2menupath" "J"
execute S:AGS2IndexUpdate "$ags2menupath" "K"
execute S:AGS2IndexUpdate "$ags2menupath" "L"
execute S:AGS2IndexUpdate "$ags2menupath" "M"
execute S:AGS2IndexUpdate "$ags2menupath" "N"
execute S:AGS2IndexUpdate "$ags2menupath" "O"
execute S:AGS2IndexUpdate "$ags2menupath" "P"
execute S:AGS2IndexUpdate "$ags2menupath" "Q"
execute S:AGS2IndexUpdate "$ags2menupath" "R"
execute S:AGS2IndexUpdate "$ags2menupath" "S"
execute S:AGS2IndexUpdate "$ags2menupath" "T"
execute S:AGS2IndexUpdate "$ags2menupath" "U"
execute S:AGS2IndexUpdate "$ags2menupath" "V"
execute S:AGS2IndexUpdate "$ags2menupath" "W"
execute S:AGS2IndexUpdate "$ags2menupath" "X"
execute S:AGS2IndexUpdate "$ags2menupath" "Y"
execute S:AGS2IndexUpdate "$ags2menupath" "Z"
echo "Done"


; End
; ---
LAB end
